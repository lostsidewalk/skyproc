
plugins {
    id 'java'
    id 'application'
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '2.7.2'
}

group = 'com.lostsidewalk'
version = '0.4-dev2'
sourceCompatibility = "11"
targetCompatibility = "11"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // This dependency is used by the application.
    implementation 'com.google.guava:guava:31.1-jre'

    implementation 'io.airlift:aircompressor:0.21'

    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '2.0.0-alpha6'

    api group: 'com.lostsidewalk', name: 'lev', version: '1.3'

    api 'org.springframework.boot:spring-boot-starter:2.7.0'

    implementation 'org.apache.commons:commons-lang3:3.12.0'

    // Use JUnit Jupiter API for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'

    // Use JUnit Jupiter Engine for testing.
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    testCompileOnly 'org.projectlombok:lombok:1.18.24'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.24'
}

application {
    mainClassName = 'skyproc.gui.SUMprogram'
}

task copyJarToBin {
    copy {
        from "build/libs/"
        include "skyproc-${project.version}.jar"
        into file('Files/')
    }
}

task renameSUMJar (type: Copy) {
    dependsOn copyJarToBin
    from ('Files/')
    include "skyproc-${project.version}.jar"
    destinationDir file('Files/')
    rename "skyproc-${project.version}.jar", "SUM.jar"
}

test {
    useJUnitPlatform()
    systemProperty "junit.jupiter.execution.parallel.enabled", false
    systemProperty "junit.jupiter.execution.mode.default", "same_thread"
    systemProperty "junit.jupiter.execution.mode.classes.default", "same_thread"
    exclude 'skyproc/SkyProcTester.class'
}


repositories {
    maven {
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/meharris-bnr/lev")
        credentials {
            username = project.findProperty("repo.user") ?: System.getenv("SKYPROC_REPO_USER")
            password = project.findProperty("repo.token") ?: System.getenv("SKYPROC_REPO_TOKEN")
        }
    }
}

publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri("https://maven.pkg.github.com/meharris-bnr/skyproc")
            credentials {
                username = project.findProperty("publishing.user") ?: System.getenv("SKYPROC_PUBLISHING_USER")
                password = project.findProperty("publishing.token") ?: System.getenv("SKYPROC_PUBLISHING_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}
